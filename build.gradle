plugins {
    id 'fabric-loom' version '0.4-SNAPSHOT' apply false
    id 'intermediary2srg' version '0.1-SNAPSHOT' apply false
    id 'maven-publish'
}

allprojects {
    apply plugin: 'java-library'

    targetCompatibility = JavaVersion.VERSION_1_8
    sourceCompatibility = JavaVersion.VERSION_1_8

    repositories {
        jcenter()
        ["snapshot", "release"].each { type ->
            maven {
                name = "minelp-$type"
                url = "https://repo.minelittlepony-mod.com/maven/$type"
            }
        }
        maven {
            name = "Forge"
            url = "https://files.minecraftforge.net/maven"
        }
        mavenLocal()
    }

    dependencies {
        compileOnly 'com.google.code.findbugs:jsr305:3.0.2'
    }
}

version = {
    def parts = [project.version]

    def branch = System.env.TRAVIS_BRANCH ?: 'master'

    if (branch != 'master') {
        parts << branch
    }

    if (branch != project.minecraft_version) {
        parts << project.minecraft_version
    }

    if (project.release != 'RELEASE') {
        parts << project.release
    }

    return parts.join('-')
}()

description = project.displayname

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
//            afterEvaluate {
//                artifact(remapJar)
//            }
//            artifact(sourcesJar) {
//                builtBy remapSourcesJar
//            }

            pom {
                name = "HDSkins"
                description = "High resolution skins for Minecraft"
                url = "https://minelittlepony-mod.com"
                licenses {
                    license {
                        name = "MIT Public License"
                        url = "https://tlo.mit.edu/learn-about-intellectual-property/software-and-open-source-licensing"

                    }
                }
                developers {
                    developer {
                        id = "killjoy1221"
                        name = "Matthew Messinger"
                        email = "mattmess1221@gmail.com"
                    }
                    developer {
                        id = "sollace"
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/MineLittlePony/HDSkins.git'
                    developerConnection = 'scm:git:ssh://github.com/MineLittlePony/HDSkins.git'
                    url = 'https://github.com/MineLittlePony/HDSkins'
                }
            }
        }
    }

    repositories {
        mavenLocal()
//        maven {
//            name = "MineLittlePony"
//            def mvn = 's3://repo.minelittlepony-mod.com/maven'
//            url = release == 'SNAPSHOT' ? "$mvn/snapshot" : "$mvn/release"
//
//            credentials(AwsCredentials) {
//                accessKey = System.env.ACCESS_KEY
//                secretKey = System.env.SECRET_KEY
//            }
//        }
    }
}
